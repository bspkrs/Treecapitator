buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

compileJava.options.encoding = 'UTF-8'

repositories {
    ivy {
        url 'http://bspk.rs/MC'
        layout 'pattern', {
            artifact "[module]/%5B${project.minecraft_version}%5D[module]-universal-[revision].[ext]"
            artifact "[module]/[${project.minecraft_version}][module]-universal-[revision].[ext]"
        }
    }
}

dependencies {
    deobfCompile name: 'bspkrsCore', version: project.bscore_version, ext: 'jar'
}

group = project.group_name
version = project.mod_version
archivesBaseName = "[${project.minecraft_version}]${project.mod_id}"

minecraft {
    version = project.minecraft_version + '-' + project.forge_version // grab latest forge
    mappings = project.mappings_version
    runDir = 'run'
    replace '@MOD_VERSION@', project.mod_version
    replace '@BSCORE_VERSION@', project.bscore_version
}

processResources {
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand ([
            'mod_version': version,
            'bscore_version': project.bscore_version,
            'forge_version': project.forge_version,
            'minecraft_version': project.minecraft_version
        ])
    }

    // copy everything else that we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

// change the name of my obfuscated jar
jar {
    appendix = project.appendix
}

sourceJar {
    appendix = 'src'
    classifier = ''
}

artifacts {
    archives sourceJar
}

if (project.hasProperty('api_key_curse') && project.hasProperty('extra_curse_id')) {
    tasks.curseforge.dependsOn signJars
    curseforge {
        apiKey = project.api_key_curse

        project {
            id = project.extra_curse_id
            changelog = ''
            releaseType = project.hasProperty('extra_curse_releasetype') ? project.extra_curse_releasetype : 'release'

            addArtifact sourceJar

            relations {
                requiredLibrary 'bspkrscore'
            }
        }
    }
}
